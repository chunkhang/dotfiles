set shell := ["zsh", "-l", "-c"]

fontello_directory := "lib/fontello"

# show help
@help:
	just --list --unsorted

# run prettier
_prettier *args:
	npx prettier {{args}}

# run eslint
_eslint *args:
	npx eslint --ext .js,.jsx {{args}}

# setup code
setup:
	npm install
	just fontello install

# run fontello
fontello *args:
	npx fontello-cli \
		--config {{fontello_directory}}/config.json \
		--css {{fontello_directory}}/css \
		--font {{fontello_directory}}/font \
		{{args}}

# lint code
lint:
	just _prettier --check .
	just _eslint .

# format code
format:
	just _prettier --write .

# fix code
fix:
	just _eslint --fix .
